# Maintainer: Philippe Proulx <pproulx@efficios.com>
# Contributor: Manuel Mendez <mmenedez534 at gmail dot com>
# Contributor: Markus Opitz <mastero23 at gmail dot com>

_pkgbase=lttng-modules
pkgname=lttng-modules-zen
pkgver=2.8.0
pkgrel=1
pkgdesc="LTTng kernel modules"
arch=('i686' 'x86_64')
url="http://lttng.org/"
license=('LGPL2.1' 'GPL2' 'MIT')
depends=('linux-zen>=2.6.38')
makedepends=('linux-zen-headers')
optdepends=(
    'lttng-tools: LTTng tracing control tools'
    'babeltrace: trace viewer'
)
install=${_pkgbase}.install
source=(http://lttng.org/files/${_pkgbase}/${_pkgbase}-${pkgver}.tar.bz2)
sha1sums=('56eeda239ce6e581036b9b72a952ac5e44d405ab')

build()
{
    cd ${srcdir}/${_pkgbase}-${pkgver}
    make
}

package()
{
    cd ${srcdir}/${_pkgbase}-${pkgver}
    make modules_install INSTALL_MOD_PATH=${pkgdir}/usr
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${_pkgbase}/LICENSE
}
